The Button Widget



Normal Buttons

We've almost seen all there is to see of the button widget. It's pretty simple. 
There is however more than one way to create a button. BeYou can use the 
[CODE]new Gtk.Button.new_with_label()[/CODE] or 
[CODE]Gtk.Button.new_with_mnemonic()[/CODE] to create a button with a label, use 
[CODE]Gtk.Button.new_from_stock()[/CODE] to create a button containing the image 
and text from a stock item or use [CODE]Gtk.Button.new()[/CODE] to create a 
blank button. It's then up to you to pack a label or pixmap into this new 
button. To do this, create a new box, and then pack your objects into this box 
using the usual [CODE]Gtk.Box.pack_start()[/CODE], and then use 
[CODE]Gtk.Container.add()[/CODE] to pack the box into the button.

Here's an example of using [CODE]Gtk.Button.new()[/CODE] to create a button 
with an image and a label in it. I've broken up the code to create a box from 
the rest so you can use it in your programs. There are further examples of using 
images later in the tutorial.

[CODE]

/* 01Button.vala - Example button. */

using Gtk;

/* Create a new hbox with an image and a label packed into it
 * and return the box. */
static Gtk.HBox xpm_label_box(string xpm_filename, 
                              string label_text ) {
  Gtk.HBox box;
  Gtk.Label label;
  Gtk.Image image;

  /* Create box for image and label */
  box = new Gtk.HBox(false, 0);
  box.set_border_width(2);

  /* Now on to the image stuff */
  image = new Gtk.Image.from_file(xpm_filename);

  /* Create a label for the button */
  label = new Gtk.Label(label_text);

  /* Pack the image and label into the box */
  box.pack_start(image, false, false, 3);
  box.pack_start(label, false, false, 3);

  image.show();
  label.show();

  return box;
}

class ButtonWindow {
  
  Gtk.Window window;
  Gtk.Button button;
  Gtk.HBox box;

  /* Our usual callback function */
  void callback (string data) {
    stdout.printf("Hello again - %s was pressed\n", data);
  }

  public ButtonWindow () {

    /* Create a new window. */
    this.window = new Gtk.Window(Gtk.WindowType.TOPLEVEL);

    this.window.set_title("Pixmap'd Buttons!");

    /* It's a good idea to do this for all windows. */
    this.window.destroy.connect( ()=> { Gtk.main_quit(); } );

    this.window.delete_event.connect( ()=> { return false; } );

    /* Sets the border width of the window. */
    this.window.set_border_width(10);

    /* Create a new button. */
    this.button = new Gtk.Button();

    /* Connect the "clicked" signal of the button to our callback. */
    this.button.clicked.connect( ()=> { this.callback("cool button"); });

    /* This calls our box creating function. */
    this.box = xpm_label_box("img.png", "cool button");

    /* Pack and show all our widgets. */
    this.box.show();

    this.button.add(box);

    this.button.show();

    this.window.add(button);

    this.window.show();
    
  }
  
  public static int main (string[] args) {
    
    Gtk.init(ref args);
    new ButtonWindow();
    
    /* Rest in gtk_main and wait for the fun to begin! */
    Gtk.main ();
    
    return 0;
  }

}

[CODE]

The [CODE]xpm_label_box()[/CODE] function could be used to pack images and labels into any widget 
that can be a container.

The Button widget has the following signals:

* pressed - emitted when pointer button is pressed within Button widget

* released - emitted when pointer button is released within Button widget

* clicked - emitted when pointer button is pressed and then released within Button widget

* enter - emitted when pointer enters Button widget

* leave - emitted when pointer leaves Button widget
